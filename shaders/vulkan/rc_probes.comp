/*
 * Stray Photons - Copyright (C) 2025 Jacob Wirth
 *
 * This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
 * If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.
 */

#version 460

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

#include "../lib/types_common.glsl"
#include "../lib/util.glsl"

layout(binding = 0) uniform sampler3D voxelRadiance;
layout(binding = 1) uniform sampler3D voxelNormals;

layout(binding = 2, rgba16f) uniform image2DArray radianceOut;

INCLUDE_LAYOUT(binding = 3)
#include "lib/view_states_uniform.glsl"

layout(binding = 4) uniform VoxelStateUniform {
    VoxelState voxelInfo;
};

layout(constant_id = 0) const int CASCADE_NUM = 0;
layout(constant_id = 1) const int BASE_SAMPLES = 4;
layout(constant_id = 2) const int NEXT_SAMPLES = 4;
layout(constant_id = 3) const float SAMPLE_LENGTH = 4;
layout(constant_id = 4) const float VOXEL_SCALE = 1;

#include "../lib/voxel_shared.glsl"
#include "lib/rc_shared.glsl"

void main() {
    if (any(greaterThanEqual(gl_GlobalInvocationID, imageSize(radianceOut)))) return;
    ivec3 xyr = ivec3(gl_GlobalInvocationID);

    ViewState view = views[0];
    vec3 viewPos = view.invViewMat[3].xyz;
	viewPos.y -= 1.3;
    vec3 viewVoxelPos = (voxelInfo.worldToVoxel * vec4(viewPos, 1.0)).xyz / vec3(voxelInfo.gridSize);

	vec2 cascadeScale = vec2(1.0) / imageSize(radianceOut).xy;

	float theta = xyr.z * 2.0 * M_PI / imageSize(radianceOut).z;
	vec2 sampleDir = vec2(sin(theta), cos(theta)) * cascadeScale;
	vec2 startPos = vec2(xyr.xy) * cascadeScale;
	vec2 nextInterval = sampleDir * SAMPLE_LENGTH;
	for (int i = 0; i < CASCADE_NUM; i++) {
		startPos += nextInterval / float(1 << (CASCADE_NUM - i));
		nextInterval *= 2;
		// nextIntervalsampleDir * SAMPLE_LENGTH * max(0, CASCADE_NUM);
	}
	vec2 endPos = startPos + nextInterval;
	// vec4 sampleValue = TraceGridLineScaled(xyr.xy, xyr.xy + sampleDir * SAMPLE_LENGTH, viewVoxelPos.y, VOXEL_SCALE);
	vec4 sampleValue = TraceSceneLine(startPos, endPos, viewVoxelPos.xz, VOXEL_SCALE * cascadeScale.x);

    imageStore(radianceOut, xyr, sampleValue);
}
